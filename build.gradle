import java.time.LocalDate
import java.time.format.DateTimeFormatter

apply plugin: 'forge'
apply plugin: 'kotlin'

buildscript {
	ext.kotlin_version = '1.9.0'

	repositories {
		mavenCentral()
		maven {
			name = 'forge'
			url = 'https://maven.minecraftforge.net/'
		}
		maven {
			name = 'sonatype'
			url = 'https://oss.sonatype.org/content/repositories/snapshots/'
		}
	}
	dependencies {
		classpath('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
			changing = true
		}
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

final def currentDate = LocalDate.now()
version = currentDate.format(DateTimeFormatter.ofPattern('yy.MM.dd'))
group = 'com.yourname.modid'
archivesBaseName = 'Genst II'

minecraft {
	version = '1.7.10-10.13.4.1614-1.7.10'
	runDir = 'run'
	srcCompat = JavaVersion.VERSION_1_8
	targetCompat = JavaVersion.VERSION_1_8
}

configurations {
	embed
	compile.extendsFrom(embed)
}

dependencies {
	embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jar {
	from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

processResources {
	inputs.property 'version', project.version
	inputs.property 'mcversion', project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

compileKotlin {
	kotlinOptions {
		jvmTarget = '1.8'
		freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'

	}
}