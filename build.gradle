import java.time.LocalDate
import java.time.format.DateTimeFormatter

buildscript {
	repositories {
		mavenCentral()
		maven {
			name = 'forge'
			url = 'https://maven.minecraftforge.net/'
		}
	}
	dependencies {
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.22'
		classpath('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
			changing = true
		}
	}
}

apply plugin: 'forge'
apply plugin: 'kotlin'

final def currentDate = LocalDate.now()
version = 'v' + currentDate.format(DateTimeFormatter.ofPattern('yy.MM.dd'))
group = 'com.yourname.modid'
archivesBaseName = 'Genst II'

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

minecraft {
	version = '1.7.10-10.13.4.1614-1.7.10'
	runDir = 'run'
}

configurations {
	embed
	compile.extendsFrom(embed)
}

dependencies {
	embed 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22'
}

jar {
	manifest {
		attributes 'Main-Class': 'genst.GenstPatchKt'
	}
	from(configurations.embed.collect { final File item ->
		if (item.isDirectory()) item else zipTree(item)
	})
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

compileKotlin {
	kotlinOptions {
		jvmTarget = '1.8'
		freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
	}
}
